    ifndef graphics_z80
    define graphics_z80

; The game's graphics is all in this file

Graphics_play_area_size: EQU (32 - 2) * (24 - 3)

; Lookup table for body segments. The tile to choose for a body segment depends
; upon:
; 1) The previous tile's direction
; 2) The current tile's direction
; To get the correct tile index, take the previous tile's direction and use
; that value as the row, then use the current tile's direction and use that
; as the column. Some combinations make no sense, e.g. Up+Down, and these
; slots contain a -1.
Body_segments:
; Turning -> Up Down Left Right   While going
    BYTE     12,  -1,  16,   19 ; Up
    BYTE     -1,  12,  17,   18 ; Down
    BYTE     18,  19,  14,   -1 ; Left
    BYTE     17,  16,  -1,   14 ; Right

; Draws a tile (bitmap + attribute) of the snake.
;   H: Y position
;   L: X position
;   A: Tile number
Draw_Snake_Tile:
    PUSH HL
    PUSH AF
    CALL Get_Char_Address
    LD DE, Snake_frames_1
    POP AF
    CALL Print_Char

    POP HL
    CALL Get_Attr_Address
    LD A, Snake_ink
    CALL Set_Ink

    RET

; Draws food's stalk at the position.
; H = Y character position
; L = X character position
Draw_Stalk:
    PUSH HL
    CALL Get_Char_Address
    LD DE, Environment_frames_1
    LD A, 1
    CALL Print_Char
    POP HL
    CALL Get_Attr_Address
    LD A, Apple_stalk_ink
    CALL Set_Ink

    RET

Draw_Apple:
    PUSH HL
    CALL Get_Char_Address
    LD A, 2
    LD DE, Environment_frames_1
    CALL Print_Char

    POP HL
    CALL Get_Attr_Address
    LD A, Apple_ink
    CALL Set_Ink
    RET

Draw_Head:
    LD HL, (Game_snake_head_x) ; H := Y position, L := X position
    CALL Game_get_address
    CALL Game_get_direction
    PUSH AF
    LD HL, (Game_snake_head_x)
    CALL Get_Char_Address
    POP AF
    PUSH AF
    ADD A, Tile_snake_head_start

    LD HL, (Game_snake_head_x) ; H := Y position, L := X position
    CALL Draw_Snake_Tile

    POP AF
    RET

; A: Current direction of the segment.
; H: Current body segment Y position
; L: Current body segment X position
Draw_snake_body_segment:
    PUSH HL
    CALL Game_get_address
    CALL Game_get_direction
    LD B, A

    ; A := B * 4
    LD A, 0
Draw_snake_body_segment_loop:
    ADD A, 4
    DJNZ Draw_snake_body_segment_loop
    LD C, A

    LD A, (Game_next_direction)
    ADD A, C

    ; Index into the lookup table, it's one-dimensional at this point.
    LD D, 0
    LD E, A
    LD HL, Body_segments
    ADD HL, DE

    LD A, (HL)

    POP HL
    CALL Draw_Snake_Tile
    RET


; Draws the neck of the snake, i.e. the body segment immediately behind the
; head.
; H Snake neck Y position
; L Snake neck X position
Draw_Neck:
    CALL Draw_snake_body_segment
    RET

; Draws the snake's tail. Takes no arguments.
Draw_Tail:

    ; Get the current tile's direction and push it on the stack.
    LD HL, (Game_snake_tail_x) ; H := Y position, L := X position
    CALL Game_get_address
    CALL Game_get_direction
    PUSH AF

    LD HL, (Game_snake_tail_x) ; As above
    CALL Get_Char_Address
    POP AF

    RLA

    ADD A, Tile_snake_tail_start

    LD HL, (Game_snake_tail_x) ; As above
    CALL Draw_Snake_Tile

    RET

        endif ; graphics_z80
