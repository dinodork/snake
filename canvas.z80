Text_Scores:
	DB 0, 0, "Snake", 0xFE
	DB 9, 0, "Score 00000000", 0xFE
	DB 24, 0, "Lives 0", 0xFF

Tile_index_brick: EQU 19

Draw_Scene:
	LD B, 32
Draw_Scene_Top_Fence_Loop:
    LD H, 1
	LD L, B
	DEC L
	CALL Get_Char_Address

	LD A, Tile_index_brick
	LD DE, Snake_1
	PUSH BC ; B gets overwritten
	CALL Print_Char
	POP BC

	DJNZ Draw_Scene_Top_Fence_Loop

	; Brick colours
	LD HL, Start_Of_Attribute_Ram + 32 ; Second row of attribute ram
	LD DE, Start_Of_Attribute_Ram + 32 + 1
	LD BC, 31 ; Repeat this many times.
	LD (HL), Ink_Yellow | Paper_Red
	LDIR

	LD B, 32
Draw_Scene_Bottom_Fence_Loop:
    LD H, 23
	LD L, B
	DEC L
	CALL Get_Char_Address

	LD A, Tile_index_brick
	LD DE, Snake_1
	PUSH BC ; B gets overwritten
	CALL Print_Char
	POP BC

	DJNZ Draw_Scene_Bottom_Fence_Loop

	LD HL, Start_Of_Attribute_Ram + 32 * 23
	LD DE, Start_Of_Attribute_Ram + 32 * 23 + 1
	LD BC, 31 ; Repeat this many times.
	LD (HL), Ink_Yellow | Paper_Red
	LDIR

	LD D, 0
	CALL Draw_Vertical_Fence
	LD D, 31
	CALL Draw_Vertical_Fence

	RET

Draw_Vertical_Fence:
	LD B, 21
Draw_Scene_Left_Fence_Loop:
    LD H, B ; Row
	LD L, D ; Column
	INC H
	CALL Get_Char_Address

	PUSH BC

	; Fill in the colour
	LD C, B
	INC C
	LD B, 0

	DUP 5 ; Multiply BC by 32
	RL BC
	EDUP

	LD IX, Start_Of_Attribute_Ram
	ADD IX, BC
	LD (IX), Ink_Yellow | Paper_Red      ; We do these two lines twice, but who
	LD (IX + 31), Ink_Yellow | Paper_Red ; cares?

	; Draw the tile
	LD IY, Snake_1 + Tile_index_brick * 8
	CALL Print_UDG8
	POP BC

	DJNZ Draw_Scene_Left_Fence_Loop

	RET
